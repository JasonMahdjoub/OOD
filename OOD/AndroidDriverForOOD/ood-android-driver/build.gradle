apply plugin: 'com.android.library'
Properties props = new Properties()
def propFile = new File(project.getProjectDir().getParentFile().getParentFile().getParentFile(), 'credentials.properties')
def artifactory_Url="https://artifactory.distri-mind.fr/artifactory"
def user=""
def pwd=""
if (propFile.canRead())
{
    props.load(new FileInputStream(propFile))


    if (props!=null && props.containsKey('artifactory_contextUrl'))
        artifactory_Url=props.get("artifactory_contextUrl")
    if (props!=null && props.containsKey('artifactory_user'))
        user=props.get("artifactory_user")
    if (props!=null && props.containsKey('artifactory_password'))
        pwd=props.get("artifactory_password")
}



repositories {


    maven {
        url "${artifactory_Url}/gradle-release"
        if (user!="")
        {
            credentials {
                username "${user}"
                password "${pwd}"
            }
        }
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += '../../src/main/java'
        main.resources.srcDirs += '../../src/main/resources'
        androidTest.java.srcDirs += '../../src/test/java'
        androidTest.resources.srcDirs += '../../src/test/resources'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}
apply from: '../../common_dependencies.gradle'
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation 'org.testng:testng:6.14.3'
    androidTestImplementation(group:'com.h2database', name:'h2', version:'1.4.200')
    implementation 'org.sqldroid:sqldroid:1.0.3'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
